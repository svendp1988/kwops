plugins {
    id "idea"
    id "io.freefair.lombok" version "8.4"
    id "io.spring.dependency-management" version "1.1.4"
    id "jacoco-report-aggregation"
    id "java"
    id "org.openapi.generator" version "6.6.0"
    id "org.springframework.boot" version "3.2.1"
}

bootJar {
    enabled = false
}

allprojects {
    apply from: "$rootDir/dependencies.gradle"
    repositories {
        mavenCentral()
    }
    group = "${versions.group}"
    version = "${versions.version}"
}

subprojects {
    java {
        sourceCompatibility = "${versions.java}"
    }

    apply from: file("$rootProject.projectDir/gradle/heroku/stage.gradle")
}

dependencies {
    jacocoAggregation project(":hr")
    jacocoAggregation project(":devops")
}

reporting {
    reports {
        testCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.UNIT_TEST
        }
    }
}

tasks.named("check") {
    dependsOn tasks.named("testCodeCoverageReport", JacocoReport)
}
